managed implementation in class zbp_i_rap_flight unique;
strict ( 2 );
with draft;

define behavior for ZI_RAP_FLIGHT alias Flight
persistent table zrap_vnq_flight
draft table zrap_d_flight
lock master
total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create;
  update;
  delete;

  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;
  field ( mandatory ) DeparturePoint, Destination;
  field ( features : instance ) FlightStatus;

  validation validateData on save { create; update; }

  determination setStatusToNew on modify { create; }

  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;

  draft determine action Prepare {
    validation validateData;
  }

  mapping for zrap_vnq_flight
  {
    FlightID = flight_id;
    DeparturePoint = departure_point;
    Destination = destination;
    DepartureTime = departure_time;
    EstimatedDuration = estimated_duration;
    FlightStatus = flight_status;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }

  association _Passengers { create; with draft; }
}

define behavior for ZI_RAP_PASSENGER alias Passenger
persistent table zrap_passngr
draft table zrap_d_passngr
lock dependent by _Flight
authorization dependent by _Flight
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly ) FlightID, LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;
  field ( mandatory ) FullName, IDNumber;

  validation validatePassenger on save { create; update; }

  determination setInitialValues on modify { create; }

  mapping for zrap_passngr
  {
    PassengerID = passenger_id;
    FlightID = flight_id;
    FullName = full_name;
    IDNumber = id_number;
    BirthDate = birth_date;
    Gender = gender;
    SeatNumber = seat_number;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }

  association _Flight { with draft; }
} 